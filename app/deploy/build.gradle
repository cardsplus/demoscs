plugins {
    id 'base'
}

import java.util.Optional;
ext.SERVER_URL = 'http://localhost'
ext.SERVER_IMAGE = Optional.ofNullable(System.getenv("SERVER_IMAGE"))
            .orElse(rootProject.name + "/server")            
ext.CLIENT_URL = 'http://localhost'
ext.CLIENT_IMAGE = Optional.ofNullable(System.getenv("CLIENT_IMAGE"))
            .orElse(rootProject.name + "/client")
            
task deployIngress {
    doLast {
        exec {
            executable 'helm'
            args 'repo', 'add', 'ingress-nginx', 'https://kubernetes.github.io/ingress-nginx'
        }
        exec {
            executable 'helm'
            args 'repo', 'update'
        }
        helmInstall('ingress-nginx', 'ingress-nginx/ingress-nginx');
        kubectlDescribeIngress();
    }
}

task deployDashboard {
    assert new File("${projectDir}/ui/dashboard.yaml").exists()
    doLast {
        kubectlApply('https://raw.githubusercontent.com/kubernetes/dashboard/v2.2.0/aio/deploy/recommended.yaml');
        kubectlApply('ui/dashboard.yaml');
     }
}

task createDashboardToken {
    assert new File("${projectDir}/ui/dashboard.yaml").exists()
    doLast {
        def userName = new ByteArrayOutputStream();
        exec {
            executable 'kubectl'
            args '-n', 'kubernetes-dashboard', 'get', 'sa/cluster-admin-user', '-o', 'jsonpath="{.secrets[0].name}"'
            standardOutput = userName
        }
        def userToken = new ByteArrayOutputStream();
        exec {
            executable 'kubectl'
            args '-n', 'kubernetes-dashboard', 'get', 'secret', userName, '-o', 'jsonpath="{.data.token}"'
            standardOutput = userToken
        }
        println '\n' + new String(userToken.toString().decodeBase64());
        println '\nhttp://localhost:8001/api/v1/namespaces/kubernetes-dashboard/services/https:kubernetes-dashboard:/proxy/'
     }
}

task deploy {
    assert new File("${projectDir}/k8s/server.yaml").exists()
    assert new File("${projectDir}/k8s/client.yaml").exists()
    assert new File("${projectDir}/k8s/ingress.yaml").exists()
    def SHA = versionDetails().gitHash
    doLast {
        dockerBuild(SERVER_IMAGE, SHA, 'server');
        dockerBuild(CLIENT_IMAGE, SHA, 'client');
        kubectlApply('k8s');
        kubectlImage(SERVER_IMAGE, SHA, 'server');
        kubectlImage(CLIENT_IMAGE, SHA, 'client');
    }
}

task deployStatus {
    doLast {
        kubectlDescribeIngress();
        kubectlDescribeService('server');
        kubectlDescribeService('client');
    }
}

task undeploy {
    doLast {
        kubectlDelete('https://raw.githubusercontent.com/kubernetes/dashboard/v2.2.0/aio/deploy/recommended.yaml');
        kubectlDelete('k8s');
        helmUninstall('ingress-nginx');
     }
}

def dockerBuild(String image, String tag, String name) {
    println "\nBuilding docker image '" + image + ":" + tag + "' ..."
    exec {
        workingDir "${projectDir}/../" + name 
        executable 'docker'
        args 'build', '.', \
            '--build-arg', 'SERVER_URL='+SERVER_URL, \
            '-t', image + ':latest', \
            '-t', image + ':' + tag
    }
}

def kubectlApply(String path) {
    println "\nApplying kubernetes objects from '" + path + "' ..."
    exec {
        executable 'kubectl'
        args 'apply', '-f', path
    }
}

def kubectlImage(String image, String tag, String name) {
    println "\nApplying docker image '" + image + ":" + tag + "' ..."
    exec {
        workingDir "${projectDir}/k8s"
        executable 'kubectl'
        args 'set', 'image', 'deployments/' + name + '-deployment', name + '=' + image + ':' + tag
    }
}

def kubectlDescribeIngress() {
    println "\n"
    exec {
        ignoreExitValue true
        executable 'kubectl'
        args 'describe', 'service', 'ingress-nginx-controller'
    }
}

def kubectlDescribeService(String name) {
    println "\n"
    exec {
        ignoreExitValue true
        executable 'kubectl'
        args 'describe', 'service', name + '-cluster-ip'
    }
}

def kubectlDelete(String path) {
    println "\nDeleting kubernetes objects from '" + path + "' ..."
    exec {
        ignoreExitValue true
        executable 'kubectl'
        args 'delete', '-f', path
    }
}

def helmInstall(String name, String chart) {
    println "\nCreating kubernetes objects from '" + name + "' ..."
    exec {
        executable 'helm'
        args 'install', name, chart
    }
}

def helmUninstall(String name) {
    println "\nDeleting kubernetes objects from '" + name + "' ..."
    exec {
        ignoreExitValue true
        executable 'helm'
        args 'uninstall', name
    }
}
