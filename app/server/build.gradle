plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.4.5'
}

dependencies {
    implementation project(':app:backend')
    // https://spring.io/projects/spring-data
    runtimeOnly('org.hsqldb:hsqldb')
}
dependencies {
    // https://projectlombok.org
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
}
dependencies {
    // https://projectlombok.org
    testImplementation('org.projectlombok:lombok')
    // https://spring.io/projects/spring-boot
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit' // by both name and group
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    // https://junit.org/junit5
    testImplementation('org.junit.jupiter:junit-jupiter')
    // https://site.mockito.org/
    testImplementation('org.mockito:mockito-junit-jupiter')
}

processResources {
    with copySpec {
        from("${rootDir}")
        include 'VERSION'
    }
}


jar {
    enabled = false
}

bootJar {
    enabled = true
    manifest {
        attributes 'Specification-Title': project.name
        attributes 'Specification-Version': VERSION
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

import java.util.Optional;
ext.IMAGE_NAME = Optional.ofNullable(System.getenv("IMAGE_NAME"))
            .orElse(System.getenv("CI_REGISTRY_IMAGE") + "/" + project.name + ":latest")

bootBuildImage {
    enabled = System.getenv().containsKey('CI_REGISTRY_IMAGE')
    imageName = IMAGE_NAME
    environment = [
        "BPL_JVM_THREAD_COUNT":"50",
        "BPL_SPRING_CLOUD_BINDINGS_ENABLED":"false"
    ]
}
