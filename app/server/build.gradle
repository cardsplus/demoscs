plugins {
    id 'java'
    id 'eclipse'
    id 'jacoco'
    id 'io.spring.dependency-management'
    id 'org.springframework.boot'
    id 'com.diffplug.spotless'
}

spotless {
    encoding 'UTF-8'
    java {
        target '**/src/**/*.java'
        target.exclude('**/build/**')
        indentWithSpaces(4)
        importOrder('', 'jakarta|javax|java', '\\#')
        removeUnusedImports()
        endWithNewline()
    }
}

dependencies {
    implementation project(':app:backend-data')
    // https://projectlombok.org
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    // https://spring.io/projects/spring-data
    runtimeOnly('org.hsqldb:hsqldb')
}
dependencies {
    testImplementation project(':app:backend-test')
    // https://projectlombok.org
    testImplementation('org.projectlombok:lombok')
    // https://spring.io/projects/spring-boot
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    // https://junit.org/junit5
    testImplementation('org.junit.jupiter:junit-jupiter')
    // https://site.mockito.org/
    testImplementation('org.mockito:mockito-junit-jupiter')
}

test {
    reports {
        html.enabled true
        html.destination = file("${rootDir}/pages/html/" + project.name + "/junit5")
    }
}

jacocoTestReport {
    mustRunAfter test
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
        html.destination file("${rootDir}/pages/html/" + project.name + "/jacoco")
    }
}

processResources {
    with copySpec {
        from("${rootDir}")
        include 'VERSION'
    }
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    manifest {
        attributes 'Specification-Title': project.name
        attributes 'Specification-Version': VERSION
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

bootBuildImage {
    enabled = false
}

import java.util.Optional;
ext.SERVER_IMAGE = Optional.ofNullable(System.getenv("SERVER_IMAGE"))
            .orElse(rootProject.name + "/" + project.name)
ext.SERVER_TAG = Optional.ofNullable(System.getenv("SERVER_TAG"))
            .orElse('latest')

task dockerBuild {
    enabled = true
    doLast {
        String image1 = SERVER_IMAGE + ":" + SERVER_TAG;
        String image2 = SERVER_IMAGE + ":" + VERSION;
        exec {
            executable 'docker'
            args 'build', '.', '-t', image1
        }
        if (image1 != image2) {
            exec {
                executable 'docker'
                args 'tag', image1, image2
            }
            println 'Built and pushed image as ' + image1 + ", " + image2
        } else {
            println 'Built and pushed image as ' + image1
        }
    }
}

task versionCheck() {
    group = 'versioning'
    build.dependsOn it
}
