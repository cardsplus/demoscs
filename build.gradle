/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 */

plugins {
    id 'base'
    id 'eclipse'
    id 'idea'
    id 'com.diffplug.spotless' version '5.10.2'
    id 'com.palantir.git-version' version '0.12.3'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

spotless {
    encoding 'UTF-8'
    java {
        target fileTree(projectDir) {
            include '**/src/main/java/esy/**/*.java'
            exclude '**/build/**'
            exclude '.gradle'
            exclude '.github'
        }
        indentWithSpaces(4)
        removeUnusedImports()
    }
}

repositories {
    jcenter()
    ruby.gems()
}

dependencies {
    asciidoctorGems 'rubygems:rouge:3.22.0'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    new File("${rootDir}/README.adoc").withReader('utf-8') { description = it.readLine().minus('= ') }
    group = 'esy.app'
    version = gitVersion()
    plugins.withType(JavaPlugin) {
        compileJava.options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    tasks.withType(Test) {
        useJUnitPlatform {
            excludeEngines 'junit-vintage'
        }
        systemProperty "file.encoding", "UTF-8"
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
            showExceptions true
            showCauses true
            showStackTraces true
            showStandardStreams false
        }
    }
}

ext.VERSION = new File("${rootDir}/VERSION").text
task versionCheck() {
    build.dependsOn it
    doLast {
        assert new File("${rootDir}/VERSION.md").text
            .startsWith("# Version " + VERSION)
    }
}

asciidoctorj {
  requires 'rouge'
  modules {
    diagram { 
      version '2.1.0'
    }
    pdf { 
      version '1.5.4'
    }
  }
  logLevel 'INFO'
  options doctype: 'book'
  attributes 'source-highlighter': 'rouge'
  attributes 'sectlink': true
  attributes 'sectanchors': true
  attributes 'numbered': false
  attributes 'idprefix': ''
  attributes 'idseparator': '-'
  attributes 'author': 'Robert Bruckbauer'
  attributes 'email':  'bruckbauer@gmx.at'
  attributes 'website': 'https://cardsplus.info'
  attributes 'revnumber': version
  attributes 'revdate': new Date().format('yyyy/MM/dd')
  attributes 'revyear': new Date().format('yyyy')
  attributes 'revremark': description
}

asciidoctor {
  dependsOn asciidoctorGemsPrepare
  baseDirFollowsSourceDir()
  attributes 'toc': 'left'
  attributes 'toclevels': '5'  
  attributes 'imagesdir': 'images'
  sourceDir file('doc')
  sources {
    include '*.adoc'
  }
  resources {
    from('doc') {
      include '**/*.jpg'
      include '**/*.png'
      include '**/*.svg'
    }
    into './images'
  }
  outputDir file('pages/html')
  outputs.upToDateWhen { false }
}

clean {
    delete "pages/html"
}